Execute:
  " Perform all selections of a text object for all lines and column positions
  " and compare the visual selection against a set of expected results given
  " per line
  function! DoAllSelections(type, expected) abort
    let cmd_template = "normal %d|v%s%s\<esc>"

    for lnum in range(1, line('$'))
      let intervals = a:expected[lnum-1]

      " Check that the list of intervals do not overlap
      for i in range(0, len(intervals)-2)
        if intervals[i][1] >= intervals[i+1][0]
          Assert false, printf(
            \'Intervals %d and %d overlap for line %d',
            \+1,
            \i+2,
            \lnum
          \)
        endif
      endfor

      " Jump to the current test line
      execute printf("normal! %dG", lnum)

      for textobj in ['a', 'i']
        for col in range(1, col('$')-1)
          execute printf(cmd_template, col, textobj, a:type)
          let result = [[lnum, col], [lnum, col]]

          for [low, high] in intervals
            if col >= low && col <= high
              let result[0][1] = low
              let result[1][1] = high
              break
            endif
          endfor

          let test_msg = printf(
            \"test %d: '%s' failed at column %d",
            \lnum,
            \getline(lnum),
            \col,
          \)

          AssertEqual getpos("'<")[1:2], result[0], test_msg
          AssertEqual getpos("'>")[1:2], result[1], test_msg
        endfor
      endfor
    endfor
  endfunction
